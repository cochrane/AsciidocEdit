:stem: latexmath

== Elimination and Intersection Theory




Define the _\(j\)-th elimination ideal_ of \(I\)
to be \(I_j = I \cap k[x_{j+1}, \ldots, x_n\)].
We eliminated the first \(j\) variables to obtain \(I_j\). Thus,
if \(I \subset k[x,y\)], then
\(I_x = I \cap k[y\)] is an elimination ideal. The ring
homomorphism 
\[
  k[y] \rightarrow k[x,y]
\] corresponds the
projection
\(\mathbb{A}^2 \rightarrow \mathbb{A}^1\) give
by projection onto the \(y\) axis.  The closure of 
the projection of \(V(I)\)
on the \(y\)-axis is the variety defined
by the elimination ideal \(I_x\).


*Theorem {counter:theorem}.* Let \(G\) be a Groebner basis
for \(I\) in the lex order.  Then \(G \cap 
k[x_{j+1}, \ldots, x_n\)]is a Groebner
basis for the \(j\)-th elimination ideal \(I_j\).


*Example {counter:example}.* Let +\(I = \left<x^3 + xy + 1,
x^2y^2,
y^4\right>\)+.  Its Groebner basis
in the  lex order is 
\(y^2, x^3 + xy + 1\).   Therefore \(I_x = \left< y^2 \right>\). 
The projection of \(V(I)\) on the \(y\)-axis is the (doubled)
origin.

The variety \(V(I)\) is easily found using the Groebner basis. 
From the first equation, we see that its points lie on the
\(x\)-axis.  Substituting \(y = 0\) in the second equation, we
find that \(x^3 = -1\).
Thus \(V(I)\) consists of three doubled points on the
math:x]-axis, while \(V(I_x)\), the projection of this variety,
consists of one double point.

Let's look at \(V(I)\) more closely.  The solutions of the
equations given by the Groebner basis are
\[
  V(I) = \{\ (-1,0), (-\omega,0), (-\omega^2, 0)\ \}
\]
Let \(K = \QQ\) and let \(L = \QQ[\omega\)].
Then \(V(K)\), the set of \(K\)-rational points,
consists of a single element.  But
\(V(L)\) has three points.   

The action of the
Galois group of \(L/K\), which is \(\ZZ/2\),
breaks \(V(L)\) into a one-point orbit
and a two-point orbit.



.Code
[source,sage]
--
sage: R.<x,y> = PolynomialRing(QQ)
sage: f = x^3 + x*y + 1
sage: g = x^2*y^2
sage: h = y^4
sage: I = Ideal(f,g,h)
sage: I.groebner_basis()
[x^3 + x*y + 1, y^2]
sage: I.dimension()
0
sage: I.vector_space_dimension()
6
--




=== Intersection of two conics

We will compute the intersection of the circle
\(x^2 + y^2 = 1\) with the ellipse \(x^2/2 + 2y^2 = 1\).  Let
\(I\) be the ideal generated by
\[
f = x^2 + y^2 - 1, \ g = x^2/2 + 2y^2 = 1.
\]
The intersection is the variety
\[
V(I) = \{\ (\pm 1/\sqrt 3, \pm \sqrt 2/\sqrt 3)\ \}
\] 
It consists of four points.  Its projection onto the \(x\)-axis,
defined by the elimination ideal \(I_y\),
consists of two points.  Thus the projection is not in general
position relative to \(V(I)\).

.Code
[source,sage]
--
R.<x,y> = PolynomialRing(QQ)
f = x^2 + y^2 - 1
g = x^2/2 + 2*y^2 - 1
I = ideal(f,g)
B = I.groebner_basis()
I.dimension(); I.vector_space_dimension(); B
0, 4, [y^2 - 1/3, x^2 - 2/3]
--

Let \(K\) be the field obtained by adjoining
a root of \(y^2 - 1/3 = 0\):  
\(K = \QQ[\sqrt 3\)]. 
Its Galois group is of order two.


Let \(L\) be the field obtained from \(K\)
by adjoining a root of \(x^2 - 2/3\):
\(L = \QQ[\sqrt 2, \sqrt 3\)].  Then \(Gal(L/\QQ) \cong
\ZZ/2\times\ZZ/2\).  Note that \(Gal(L/\QQ)\) acts transitively on
\(V(L)\), and that
\(Gal(K/\QQ)\) permutes the fibers of the projection.



=== Intersection of a conic and a quintic

Let's study the intersection of the unit circle and the 
quintic \(x^5/2 + 2y^5 = 1\).   Thus let \(I = \left<x^2 + y^2 -
1, x^5/2 + 2x^5 - 1\right>\)
We find that \(I\) has dimension zero, and the \(\QQ[x,y\)/I]
is a vector space of dimension ten.
The Groebner basis is
----
[ y^10 - 5/17*y^8 + 10/17*y^6 - 16/17*y^5 - 10/17*y^4 + 
     5/17*y^2 + 3/17,
  x + 238/9*y^9 + 170/9*y^8 + 13*y^7 + 21/2*y^6 + 221/9*y^5
     - 95/18*y^4 - 145/9*y^3 - 265/18*y^2 - 17/3*y - 35/6  ]
----

It has the form \(F(y), x + G(y)\), where \(I_x =
\left<F(y)\right>\).  From the form of the
Groebner basis we see that projection onto 
the \(y\)-axis is generic.
Also, the Galois group of \(F(y)\) gives the
action of \(Gal(\bar \QQ/\QQ)\) on \(V(\bar\QQ)\).
Assuming \(G\) is the Groebner basis in \(R = \QQ[x,y\)], 
we compute the Galois group:

.Code
[source,sage]
--
S.<u> = PolynomialRing(QQ)
phi = R.hom([0,u], S)
h = phi(B[0])
K.<a> = NumberField(h/h.leading_coefficient())
K.galois_group()
Galois group PARI group [3628800, -1, 45, "S10"] 
of degree 10 of the number field Number Field 
in a with defining polynomial ...
--

Thus \(Gal(\bar \QQ/\QQ)\) acts on \(V(\bar \QQ)\)
through \(Gal(K/\QQ) \cong S_{10}\). 
And \(V(\bar \QQ) = V(K)\).



=== Lines on the cubic surface


As an illustration of what we have done so far,
we will compute the number of lines on a cubic surface \(S\). 
This will be a warmup for our study of the Elsenhans-Jahnel
paper.  There they study the image of the group \(Gal( \bar\QQ/
\QQ )\)
one the set of lines \({\mathcal L}\) of \(S\).
It is known that that the image is a subgroup of \(W(E_6)\).
It is also known that generically the image is \(W(E_6)\).
The Elsenhans-Jahnel algorithm allows us to exihibit
specific cubic surfaces with maximal Galois image.
See http://www.uni-math.gwdg.de/jahnel/linkstopaperse.html


==== Sage program

We will write a program {\tt lines.sage} that we can execute
at the commmand line: {\tt sage lines.sage 2 3 5 7}.
This command will attempt to find the Galois image for 
\(x^3 + y^3 + z^3 + w^3 + 2yzw + 3xzw + 5xyw + 7xyz\)

.Code
[source,python]
--
# file: lines.sage
import sys

K = QQ
R.<a,b,c,d> = PolynomialRing(K, order = "lex")
S.<x,y,z,w> = PolynomialRing(K)

p1, p2, p3, p4 = map( lambda x: int(x), sys.argv[1:] )
F = x^3 + y^3 + z^3 + w^3 + 
      p1*y*z*w + p2*x*z*w + p3*x*y*w + p4*x*y*z
print "F =", F

J = ideal(F.jacob())
pd = J.dimension() - 1
print "Projective dimension of singular locus =", pd

L = lambda t: (1,t,a+b*t,c+d*t)
FL = lambda t: F(L(t))
J = ideal(FL(0), FL(1), FL(2), FL(3) )
B = J.groebner_basis()

print "Variables on which g[i] depends:"

for i in range(0,len(B)):
  print i, B[i].variables()

g = B[0]; dg = g.degree(); nf = len(factor(g))

print "degree(g) =", g.degree()
print "number of factors of g =", len(factor(g))

if nf == 1 and dg == 27:
	print "cubic surface has 27 lines"
--

.Session
[source,sage]
--
$ sage lines.sage 2 3 5 7
F = x^3 + y^3 + 7*x*y*z + z^3 + 
      5*x*y*w + 3*x*z*w + 2*y*z*w + w^3
Projective dimension of singular locus = -1
Variables on which g[i] depends:
0 [d]
1 [d, c]
2 [d, b]
3 [d, a]
degree(g) = 27
number of factors of g = 1
cubic surface has 27 lines
--




----
1631363571648*d^27 + 25828675997904*d^26 + 
174058261443792*d^25 + 670420268514936*d^24 + 
1676405524223304*d^23 + 2975484187123416*d^22 + 
4782940362665136*d^21 + 10123074083576106*d^20 + 
23578095466184706*d^19 + 40873449674344679*d^18 + 
49700044180519437*d^17 + 56665434448534617*d^16 +
91887374548676657*d^15 + 153951454933532346*d^14 + 
190706504766850386*d^13 + 140074290975822037*d^12 + 
53432695230612333*d^11 + 66098863812061161*d^10 +
127572387825582827*d^9 + 97842438970193286*d^8 + 
27880556381159262*d^7 + 14563730364420960*d^6 + 
26236696128820584*d^5 + 18211964913020760*d^4 +
5315592754434120*d^3 + 944024070738000*d^2 + 
34293720251600*d - 47674198376000
----


Why can't we just look at the number field that \(g\) defines and
compute the
Galois group directly using Sage?


Answer: \(<\)type 'exceptions.NotImplementedError'\(>\): Sorry,
computation of Galois groups of fields of degree bigger than 11 is
not yet implemented.  Try installing the optional free (closed
source) KASH package, which supports up to degree \(23\).